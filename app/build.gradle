plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp' version '1.7.10-1.0.6'
    id 'scabbard.gradle'
}

scabbard {
    enabled true
    failOnError true
    fullBindingGraphValidation true
    qualifiedNames false
    outputFormat "svg"
}

android {
    def APP_NAME = 'app_name'
    def BASE_URL = 'BASE_URL'

    def STRING = 'string'
    def STRING_UPPER = 'String'

    namespace 'com.far.goestochecklist'
    compileSdk 33

    defaultConfig {
        applicationId "com.far.goestochecklist"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        getByName("release") {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue STRING, APP_NAME, "Goes to - Checklist"
            buildConfigField STRING_UPPER, BASE_URL, '"https://goes-to-ckecklist.herokuapp.com"'
        }
        getByName("debug") {
            applicationIdSuffix ".debug"
            resValue STRING, APP_NAME, "[DEV] Goes to - Checklist"
            buildConfigField STRING_UPPER, BASE_URL, '"https://goes-to-checklist-api-stg.herokuapp.com"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.3'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    // Core
    implementation "androidx.core:core-ktx:$core_ktx"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_runtime"
    implementation "androidx.core:core-splashscreen:$splash_screen"
    implementation "com.jakewharton.timber:timber:$timber"
    implementation "com.google.code.gson:gson:$gson"

    // Compose dependencies
    implementation "androidx.compose.material:material:$compose_material"
    implementation "androidx.compose.material:material-icons-extended:$compose_material"
    implementation "androidx.activity:activity-compose:$compose_activity"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_viewmodel"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.navigation:navigation-compose:$compose_navigation"
    implementation "me.onebone:toolbar-compose:$compose_collapsing_toolbar"
    implementation "com.google.accompanist:accompanist-navigation-animation:$compose_navigation_animation"

    //Dagger - Hilt
    implementation "com.google.dagger:hilt-android:$dagger_hilt"
    kapt "com.google.dagger:hilt-android-compiler:$dagger_hilt"
    kapt "androidx.hilt:hilt-compiler:$dagger_hilt_compiler"
    implementation "androidx.hilt:hilt-navigation-compose:$dagger_hilt_navigation"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.okhttp3:okhttp:$okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp"

    // Room
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    kapt "androidx.room:room-compiler:$room"

    // Coil
    implementation "io.coil-kt:coil-compose:$coil"

    // Tests
    testImplementation "junit:junit:$junit"

    androidTestImplementation "androidx.test.ext:junit:$junit_ext"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}